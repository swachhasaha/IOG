Terraform version-1

Final working code: 1.  main.tf
terraform {
required_version = ">= 1.4"
required_providers {
aws = {
source = "hashicorp/aws"
version = ">= 5.0"
}
}
}
provider "aws" {
region = "ap-south-1"
}
variable "key_name" {
type = string
description = "EC2 key pair name"
}
variable "ecr_image_url" {
type = string
description = "Image to use in ECS container"
}
variable "db_password" {
type = string
description = "Password for RDS"
sensitive = true
}
###################### VPC ######################
module "vpc" {
source = "terraform-aws-modules/vpc/aws"
version = "~> 5.0"
name = "revsites-vpc"
cidr = "10.0.0.0/16"
azs = ["ap-south-1a", "ap-south-1b"]
public_subnets = ["10.0.1.0/24", "10.0.2.0/24"]
private_subnets = ["10.0.11.0/24", "10.0.12.0/24"]
enable_nat_gateway = true
single_nat_gateway = true
enable_dns_support = true
enable_dns_hostnames = true
}
###################### Security Groups ######################
resource "aws_security_group" "alb_sg" {
name = "alb-sg"
vpc_id = module.vpc.vpc_id
ingress {
from_port = 80
to_port = 80
protocol = "tcp"
cidr_blocks = ["0.0.0.0/0"]
}
egress {
from_port = 0
to_port = 0
protocol = "-1"
cidr_blocks = ["0.0.0.0/0"]
}
}
resource "aws_security_group" "ecs_sg" {
name = "ecs-sg"
vpc_id = module.vpc.vpc_id
ingress {
from_port = 80
to_port = 80
protocol = "tcp"
security_groups = [aws_security_group.alb_sg.id]
}
egress {
from_port = 0
to_port = 0
protocol = "-1"
cidr_blocks = ["0.0.0.0/0"]
}
}
resource "aws_security_group" "rds_sg" {
name = "rds-sg"
vpc_id = module.vpc.vpc_id
ingress {
from_port = 5432
to_port = 5432
protocol = "tcp"
security_groups = [aws_security_group.ecs_sg.id]
}
egress {
from_port = 0
to_port = 0
protocol = "-1"
cidr_blocks = ["0.0.0.0/0"]
}
}
###################### IAM Roles ######################
resource "aws_iam_role" "ecs_instance_role" {
name = "ecs-instance-role"
assume_role_policy = jsonencode({
Version = "2012-10-17",
Statement = [{
Effect = "Allow",
Principal = { Service = "ec2.amazonaws.com" },
Action = "sts:AssumeRole"
}]
})
}
resource "aws_iam_role_policy_attachment" "ecs_instance_attach" {
role = aws_iam_role.ecs_instance_role.name
policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
}
resource "aws_iam_instance_profile" "ecs_profile" {
name = "ecs-profile"
role = aws_iam_role.ecs_instance_role.name
}
resource "aws_iam_role" "ecs_exec_role" {
name = "ecs-exec-role"
assume_role_policy = jsonencode({
Version = "2012-10-17",
Statement = [{
Effect = "Allow",
Principal = { Service = "ecs-tasks.amazonaws.com" },
Action = "sts:AssumeRole"
}]
})
}
resource "aws_iam_role_policy_attachment" "ecs_exec_attach" {
role = aws_iam_role.ecs_exec_role.name
policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}
###################### ECS LT + ASG + Cluster ######################
data "aws_ami" "ecs_ami" {
most_recent = true
owners = ["amazon"]
filter {
name = "name"
values = ["amzn2-ami-ecs-hvm-*-x86_64-ebs"]
}
}
resource "aws_launch_template" "ecs_lt" {
name_prefix = "ecs-lt-"
image_id = data.aws_ami.ecs_ami.id
instance_type = "t3.micro"
key_name = var.key_name
iam_instance_profile {
name = aws_iam_instance_profile.ecs_profile.name
}
vpc_security_group_ids = [aws_security_group.ecs_sg.id]
user_data = base64encode(<<-EOF
#!/bin/bash
echo "ECS_CLUSTER=revsites-dev" >> /etc/ecs/ecs.config
EOF
)
}
resource "aws_autoscaling_group" "ecs_asg" {
name = "ecs-asg"
max_size = 2
min_size = 1
desired_capacity = 1
vpc_zone_identifier = module.vpc.private_subnets
health_check_type = "EC2"
force_delete = true
protect_from_scale_in = true
launch_template {
id = aws_launch_template.ecs_lt.id
version = "$Latest"
}
tag {
key = "Name"
value = "ecs-instance"
propagate_at_launch = true
}
}
resource "aws_ecs_capacity_provider" "cp" {
name = "revsites-cp"
auto_scaling_group_provider {
auto_scaling_group_arn = aws_autoscaling_group.ecs_asg.arn
managed_termination_protection = "ENABLED"
managed_scaling {
status = "ENABLED"
target_capacity = 85
}
}
}
resource "aws_ecs_cluster" "cluster" {
name = "revsites-dev"
}
resource "aws_ecs_cluster_capacity_providers" "cp_attach" {
cluster_name = aws_ecs_cluster.cluster.name
capacity_providers = [aws_ecs_capacity_provider.cp.name]
default_capacity_provider_strategy {
capacity_provider = aws_ecs_capacity_provider.cp.name
weight = 1
base = 1
}
}
###################### ECS Task + Service + ALB + RDS ######################
resource "aws_cloudwatch_log_group" "ecs_log" {
name = "/ecs/api-client"
retention_in_days = 7
}
resource "aws_ecs_task_definition" "task" {
family = "api-client"
network_mode = "bridge"
requires_compatibilities = ["EC2"]
cpu = "256"
memory = "512"
execution_role_arn = aws_iam_role.ecs_exec_role.arn
container_definitions = jsonencode([{
name = "api-client",
image = var.ecr_image_url,
cpu = 128,
memory = 256,
essential = true,
portMappings = [{
containerPort = 80,
hostPort = 80
}],
logConfiguration = {
logDriver = "awslogs",
options = {
awslogs-group = aws_cloudwatch_log_group.ecs_log.name,
awslogs-region = "ap-south-1",
awslogs-stream-prefix = "ecs"
}
}
}])
}
resource "aws_lb" "alb" {
name = "revsites-alb"
internal = false
load_balancer_type = "application"
security_groups = [aws_security_group.alb_sg.id]
subnets = module.vpc.public_subnets
}
resource "aws_lb_target_group" "tg" {
name = "api-client-tg"
port = 80
protocol = "HTTP"
vpc_id = module.vpc.vpc_id
target_type = "instance"
}
resource "aws_lb_listener" "listener" {
load_balancer_arn = aws_lb.alb.arn
port = 80
protocol = "HTTP"
default_action {
type = "forward"
target_group_arn = aws_lb_target_group.tg.arn
}
}
resource "aws_ecs_service" "service" {
name = "api-client"
cluster = aws_ecs_cluster.cluster.id
task_definition = aws_ecs_task_definition.task.arn
desired_count = 2
launch_type = "EC2"
load_balancer {
target_group_arn = aws_lb_target_group.tg.arn
container_name = "api-client"
container_port = 80
}
ordered_placement_strategy {
type = "binpack"
field = "cpu"
}
depends_on = [aws_lb_listener.listener]
}
resource "aws_db_subnet_group" "rds" {
name = "rds-subnet-group"
subnet_ids = module.vpc.private_subnets
}
resource "aws_db_instance" "postgres" {
identifier = "revsites-postgres"
engine = "postgres"
instance_class = "db.t3.micro"
allocated_storage = 20
username = "postgres"
password = var.db_password
db_subnet_group_name = aws_db_subnet_group.rds.name
vpc_security_group_ids = [aws_security_group.rds_sg.id]
skip_final_snapshot = true
publicly_accessible = false
} 
2. terraform.tfvars
key_name = "terraform-key1"
ecr_image_url = "nginx:latest"
db_password = "Passw0rd2050"
3.
Workspace variables (4)
Variables defined within a workspace always overwrite variables from variable sets that have the same type and the same key. Learn more about variable set precedence.
Key
Value
Category
Actions
db_password
Sensitive
Sensitive - write only
terraform


ecr_image_url
public.ecr.aws/nginx/nginx:stable-perl
terraform


AWS_ACCESS_KEY_ID
Sensitive
Sensitive - write only
env


AWS_SECRET_ACCESS_KEY
Sensitive
Sensitive - write only
env





Terraform version-2
terraform {
  required_version = ">= 1.4"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = ">= 5.0"
    }
  }
}

provider "aws" {
  region = "ap-south-1"
}

variable "key_name" {
  type        = string
  description = "EC2 key pair name"
}

variable "ecr_image_url" {
  type        = string
  description = "Image to use in ECS container"
}

variable "db_password" {
  type        = string
  description = "Password for RDS"
  sensitive   = true
}

###################### VPC ######################
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "~> 5.0"

  name = "revsites-vpc"
  cidr = "10.0.0.0/16"

  azs              = ["ap-south-1a", "ap-south-1b"]
  public_subnets   = ["10.0.1.0/24", "10.0.2.0/24"]
  private_subnets  = ["10.0.11.0/24", "10.0.12.0/24"]

  enable_nat_gateway   = true
  single_nat_gateway   = true
  enable_dns_support   = true
  enable_dns_hostnames = true
}

###################### Security Groups ######################
resource "aws_security_group" "alb_sg" {
  name   = "alb-sg"
  vpc_id = module.vpc.vpc_id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "ecs_sg" {
  name   = "ecs-sg"
  vpc_id = module.vpc.vpc_id

  ingress {
    from_port       = 80
    to_port         = 80
    protocol        = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "rds_sg" {
  name   = "rds-sg"
  vpc_id = module.vpc.vpc_id

  ingress {
    from_port       = 5432
    to_port         = 5432
    protocol        = "tcp"
    security_groups = [aws_security_group.ecs_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

###################### IAM Roles ######################
resource "aws_iam_role" "ecs_instance_role" {
  name = "ecs-instance-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Principal = { Service = "ec2.amazonaws.com" },
      Action    = "sts:AssumeRole"
    }]
  })
}

resource "aws_iam_role_policy_attachment" "ecs_instance_attach" {
  role       = aws_iam_role.ecs_instance_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
}

resource "aws_iam_instance_profile" "ecs_profile" {
  name = "ecs-profile"
  role = aws_iam_role.ecs_instance_role.name
}

resource "aws_iam_role" "ecs_exec_role" {
  name = "ecs-exec-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Principal = { Service = "ecs-tasks.amazonaws.com" },
      Action    = "sts:AssumeRole"
    }]
  })
}

resource "aws_iam_role_policy_attachment" "ecs_exec_attach" {
  role       = aws_iam_role.ecs_exec_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

###################### ECS LT + ASG + Cluster ######################
data "aws_ami" "ecs_ami" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-ecs-hvm-*-x86_64-ebs"]
  }
}

resource "aws_launch_template" "ecs_lt" {
  name_prefix   = "ecs-lt-"
  image_id      = data.aws_ami.ecs_ami.id
  instance_type = "t3.medium"
  key_name      = var.key_name

  iam_instance_profile {
    name = aws_iam_instance_profile.ecs_profile.name
  }

  vpc_security_group_ids = [aws_security_group.ecs_sg.id]

  user_data = base64encode(<<-EOF
    #!/bin/bash
    echo "ECS_CLUSTER=revsites-dev" >> /etc/ecs/ecs.config
  EOF
  )
}

resource "aws_autoscaling_group" "ecs_asg" {
  name                      = "ecs-asg"
  max_size                  = 2
  min_size                  = 1
  desired_capacity          = 1
  vpc_zone_identifier       = module.vpc.private_subnets
  health_check_type         = "EC2"
  force_delete              = true
  protect_from_scale_in     = true

  launch_template {
    id      = aws_launch_template.ecs_lt.id
    version = "$Latest"
  }

  tag {
    key                 = "Name"
    value               = "ecs-instance"
    propagate_at_launch = true
  }
}

resource "aws_ecs_capacity_provider" "cp" {
  name = "revsites-cp"

  auto_scaling_group_provider {
    auto_scaling_group_arn         = aws_autoscaling_group.ecs_asg.arn
    managed_termination_protection = "ENABLED"

    managed_scaling {
      status          = "ENABLED"
      target_capacity = 85
    }
  }
}

resource "aws_ecs_cluster" "cluster" {
  name = "revsites-dev"
}

resource "aws_ecs_cluster_capacity_providers" "cp_attach" {
  cluster_name       = aws_ecs_cluster.cluster.name
  capacity_providers = [aws_ecs_capacity_provider.cp.name]

  default_capacity_provider_strategy {
    capacity_provider = aws_ecs_capacity_provider.cp.name
    weight            = 1
    base              = 1
  }
}

###################### ECS Task + Service + ALB + RDS ######################
resource "aws_cloudwatch_log_group" "ecs_log" {
  name              = "/ecs/api-client"
  retention_in_days = 7
}

resource "aws_ecs_task_definition" "task" {
  family                   = "api-client"
  network_mode             = "bridge"
  requires_compatibilities = ["EC2"]
  cpu                      = "256"
  memory                   = "512"
  execution_role_arn       = aws_iam_role.ecs_exec_role.arn

  container_definitions = jsonencode([{
    name      = "api-client",
    image     = var.ecr_image_url,
    cpu       = 128,
    memory    = 256,
    essential = true,
    portMappings = [{
      containerPort = 80,
      hostPort      = 80
    }],
    logConfiguration = {
      logDriver = "awslogs",
      options = {
        awslogs-group         = aws_cloudwatch_log_group.ecs_log.name,
        awslogs-region        = "ap-south-1",
        awslogs-stream-prefix = "ecs"
      }
    }
  }])
}

resource "aws_lb" "alb" {
  name               = "revsites-alb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [aws_security_group.alb_sg.id]
  subnets            = module.vpc.public_subnets
}

resource "aws_lb_target_group" "tg" {
  name        = "api-client-tg"
  port        = 80
  protocol    = "HTTP"
  vpc_id      = module.vpc.vpc_id
  target_type = "instance"
}

resource "aws_lb_listener" "listener" {
  load_balancer_arn = aws_lb.alb.arn
  port              = 80
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg.arn
  }
}

resource "aws_ecs_service" "service" {
  name            = "api-client"
  cluster         = aws_ecs_cluster.cluster.id
  task_definition = aws_ecs_task_definition.task.arn
  desired_count   = 2
  launch_type     = "EC2"

  load_balancer {
    target_group_arn = aws_lb_target_group.tg.arn
    container_name   = "api-client"
    container_port   = 80
  }

  ordered_placement_strategy {
    type  = "binpack"
    field = "cpu"
  }

  depends_on = [aws_lb_listener.listener]
}

resource "aws_db_subnet_group" "rds" {
  name       = "rds-subnet-group"
  subnet_ids = module.vpc.private_subnets
}

resource "aws_db_instance" "postgres" {
  identifier              = "revsites-postgres"
  engine                  = "postgres"
  instance_class          = "db.t3.micro"
  allocated_storage       = 20
  username                = "postgres"
  password                = var.db_password
  db_subnet_group_name    = aws_db_subnet_group.rds.name
  vpc_security_group_ids  = [aws_security_group.rds_sg.id]
  skip_final_snapshot     = true
  publicly_accessible     = false
}
